plugins {
    id 'java-library'
    id 'maven-publish'
    id 'org.jreleaser' version '1.15.0'
}

repositories {
    mavenCentral()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    maven { url "https://plugins.gradle.org/m2/" }
}


group = 'io.github.lily-es.remark'
version = '1.2.2'
archivesBaseName = "remark"
sourceCompatibility = 1.8
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

dependencies {
    implementation 'org.jsoup:jsoup:1.18.3'
    implementation "org.apache.commons:commons-lang3:3.17.0"
    implementation "commons-cli:commons-cli:1.9.0"
    testImplementation 'commons-io:commons-io:2.16.1'
    testImplementation group: 'junit', name: 'junit', version: '4.13.2'
}

java {
    withJavadocJar()
    withSourcesJar()
}


publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'io.github.lily-es'
            artifactId = 'remark'

            from components.java

            pom {
                name = 'remark'
                description = 'Remark is a library for taking (X)HTML input and outputting clean Markdown, Markdown Extra, or MultiMarkdown compatible text.'
                url = 'https://github.com/lily-es/remark-java/'
                inceptionYear = "2011"

                scm {
                    connection = 'scm:git:git@github.com:lily-es/remark-java.git'
                    developerConnection = 'scm:git:git@github.com:lily-es/remark-java.git'
                    url = 'git@github.com:lily-es/remark-java.git'
                }

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        name = 'Phil DeJarnett'
                        url = 'http://www.overzealous.com'
                    }
                    developer {
                        name = 'Guilherme I F L Weizenmann'
                        url = 'http://itquasar.com'
                    }
                    developer {
                        id = 'kotcrab'
                        name = 'Pawel Pastuszak'
                        url = 'https://kotcrab.com'
                    }
                    developer {
                        id = 'lilyes'
                        name = 'Lily Espirito Santo'
                        url = 'https://github.com/lily-es'
                    }
                }
            }
        }
    }

    repositories {
        maven {
            url = layout.buildDirectory.dir('staging-deploy')
        }
    }
}

jreleaser {
    signing {
        active = 'ALWAYS'
        armored = true
    }
    deploy {
        maven {
            mavenCentral {
                sonatype {
                    active = 'ALWAYS'
                    url = 'https://central.sonatype.com/api/v1/publisher'
                    stagingRepository('build/staging-deploy')
                }
            }
        }
    }
}

if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}
